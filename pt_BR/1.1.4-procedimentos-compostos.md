# 1.1.4 Procedimentos Compostos

Nós indentificamos em Lisp alguns dos elentos que devem aparecer em qualquer linguagem poderosa:

- Números e operações aritméticas são dados e procedimentos primitivos.
- Aninhamento de combinações fornece um meio de combinar operações.
- Definições que associam nomes a valores fornecem um meio limitado de abstração.

Agora aprenderemos sobre *definições de procedimentos*, uma técnica de abstração muito mais poderosa, através da qual uma operação composta pode receber um nome e ser referida como uma unidade.

Começamos verificando como expressar a idéia de "quadrado". Podemos dizer: "Para elevar algo ao quadrado, multiplique ele por ele mesmo." Isso é expressado em nossa linguagem como

``` {.scheme}
(define (square x) (* x x))
```

Podemos entender isso da seguinte maneira:

``` {.example}
(define (square x)    (*       x       x))
  |      |      |      |       |       |
 ao quadrado algo, multiplica por si mesmo.
```

Temos aqui um *procedimento composto*, que recebeu o nome `square`. O procedimento representa a operação de multiplicar algo por si mesmo. A coisa a ser multiplicada recebe um nome local, `x`, que desempenha o mesmo papel que um pronome desempenha na linguagem natural. Avaliar a definição cria este procedimento composto e associa-o ao nome `square`.

A forma geral de uma definição de procedimento é

``` {.scheme}
(define (⟨name⟩ ⟨formal parameters⟩) ⟨body⟩)
```

O `(`name`)` é um símbolo a ser associado a definição do procedimento no ambiente.

Tendo definido o `square`, agora podemos usá-lo:

``` {.scheme}
(square 21)
441

(square (+ 2 5))
49

(square (square 3))
81
```

Também podemos usar o `square` como um bloco de construção na definição de outros procedimentos. Por exemplo, $x^{2} + y^{2}$ pode ser expressado como

``` {.scheme}
(+ (square x) (square y))
```

Podemos definir facilmente um procedimento `sum-of-squares` que, dados quaisquer dois números como argumentos, produz a soma de seus quadrados:

``` {.scheme}
(define (sum-of-squares x y)
  (+ (square x) (square y)))

(sum-of-squares 3 4)
25
```

Agora podemos usar `sum-of-squares` como um bloco na construção de outros procedimentos:

``` {.scheme}
(define (f a)
  (sum-of-squares (+ a 1) (* a 2)))

(f 5)
136
```

Procedimentos compostos são usados exatamente da mesma maneira que procedimentos primitivos. De fato, não se pode dizer, olhando para a definição de sum-of-squares, dada acima, se o `square` foi construído no interpretador, como `+` e `*`, ou definido como um procedimento composto.
