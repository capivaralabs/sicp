## 1.1.6 Expressões Condicionais e Predicados

O poder expressivo da classe de procedimentos que podemos definir nesse momento é muito limitado, porque não temos como fazer testes e executar operações diferentes dependendo do resultado de um teste. Por exemplo, não podemos definir um procedimento que calcula o valor absoluto de um número testando se o número é positivo, negativo ou zero e tomando ações diferentes nos diferentes casos, de acordo com a regra.

<img src="../fig/1/cond.svg" width=250 >

Essa construção é chamada *análise de caso*, e há um formulário especial no Lisp para anotar essa análise de caso. É chamado `cond` (que significa “condicional”), e é usado da seguinte maneira:

``` {.scheme}
(define (abs x)
  (cond ((> x 0) x)
        ((= x 0) 0)
        ((< x 0) (- x))))
```

A forma geral de uma expressão condicional é

``` {.scheme}
(cond (⟨p₁⟩ ⟨e₁⟩)
      (⟨p₂⟩ ⟨e₂⟩)
      …
      (⟨pₙ⟩ ⟨eₙ⟩))
```

consistindo no símbolo `cond` seguido por pares de expressões entre parênteses

``` {.scheme}
(⟨p⟩ ⟨e⟩)
```

chamado *cláusulas*. A primeira expressão em cada par é um *predicado* - isto é, uma expressão cujo valor é interpretado como verdadeiro ou falso.

Expressões condicionais são avaliadas da seguinte maneira. O predicado <p₁> é avaliado primeiro. Se seu valor é falso, então <p₁> é avaliado. Se o valor de <p₂> também é falso, então <p₃> é avaliado. Esse processo continua até que um predicado seja encontrado, cujo valor é verdadeiro, nesse caso o interpretador retorna o valor da expressão *consequente* <e\> correspondente da cláusula como o valor da expressão condicional. Se nenhum dos <p\> é encontrado como verdadeiro, o valor do `cond` é indefinido.

A palavra *predicado* é usada para procedimentos que retornam verdadeiro ou falso, bem como para expressões que são avaliadas como verdadeiras ou falsas. O procedimento de valor absoluto `abs` faz uso dos predicados primitivos `>`, `<`, e `=`.

Outra maneira de escrever o procedimento de valor absoluto é

``` {.scheme}
(define (abs x)
  (cond ((< x 0) (- x))
        (else x)))
```

que poderia ser expresso em Portugues como “Se x é ḿenor que zero retorna - x; caso contrário, retorne x.”

Aqui está mais uma maneira de escrever o procedimento de valor absoluto:

``` {.scheme}
(define (abs x)
  (if (< x 0)
      (- x)
      x))
```

isso usa a formulário especial `if`, um tipo restrito de condicional que pode ser usado quando há precisamento dois casos na análise de caso. A forma geral de uma expressão `if` é

``` {.scheme}
(if ⟨predicate⟩ ⟨consequent⟩ ⟨alternative⟩)
```

Para avaliar uma expressão `if`, O interpretador começa avaliado parte do `⟨`predicado`⟩` da expressão. Se o `⟨`predicate`⟩` for avaliado como um valor verdadeiro, o intérprete então avaliará o `⟨`consequente`⟩` e retornará seu valor. caso contrário ele avalia o `⟨`alternativo`⟩` e retorna seu valor.

Além dos predicados primitivos, como `<`, `=` e `>`, existem operações de composição lógica, que nos permitem construir predicados compostos. Os três mais utilizados são estes:

- `(and ⟨e₁⟩ … ⟨eₙ⟩)`

    O intérprete avalia as expressoes `⟨`e`⟩` uma de cada vez, da esquerda para direita. Se algum `⟨`e`⟩` for falso, o valor da expressão `and` é falso, e o resto do `⟨`e`⟩` não é avaliado. Se todos os `⟨`e`⟩` forem avaliados para valores verdadeiros, o valor da expressão `and` é o valor do último.

- `(or ⟨e₁⟩ … ⟨eₙ⟩)`

    O intérprete avalia as expressões `⟨`e`⟩` uma de cada vez, da esquerda para a direita. Se qualquer `⟨`e`⟩` é avaliado como um valor verdadeiro, esse valor é retornado como o valor da expressão `or`, e o resto do `⟨`e`⟩` não é avaliado. Se todos os `⟨`e`⟩` são avaliados como falso, o valor da expressão `or` é falso.

- `(not ⟨e⟩)`

    O valor de uma expressão `not` é verdadeiro quando a expressão `⟨`e`⟩` é avaliada como falsa e, caso contrário, o valor é falso

Observe que `and` e `or` são formas especiais, não procedimentos, porque as subexpressões não são necessariamente todas avaliadas. `Não` é um procedimento comum.

Como um exemplo de como eles são usados, a condição de que um número x esteja no intervalo 5 < x < 10 pode ser expresso como

``` {.scheme}
(and (> x 5) (< x 10))
```

Como outro exemplo, podemos definir um predicado para testar se um número é maior ou igual a outro como

``` {.scheme}
(define (>= x y) 
  (or (> x y) (= x y)))
```

ou alternativamente como

``` {.scheme}
(define (>= x y) 
  (not (< x y)))
```

**Exercício 1.1:** Abaixo está uma sequência de expressões. Qual é o resultado impresso pelo intérprete em resposta a cada expressão? Suponha que a sequência seja avaliada na ordem em que é apresentada.


``` {.scheme}
10
(+ 5 3 4)
(- 9 1)
(/ 6 2)
(+ (* 2 4) (- 4 6))
(define a 3)
(define b (+ a 1))
(+ a b (* a b))
(= a b)
(if (and (> b a) (< b (* a b)))
    b
    a)
(cond ((= a 4) 6)
      ((= b 4) (+ 6 7 a))
      (else 25))
(+ 2 (if (> b a) b a))
(* (cond ((> a b) a)
         ((< a b) b)
         (else -1))
   (+ a 1))
```


**Exercício 1.2:** Traduza a seguinte expressão para o formato de prefixo:

<img src="../fig/1/1.2-exercise.svg" width=250 >  

**Exercício 1.3:** Defina um procedimento que tome três números como argumentos e retorne a soma dos quadrados dos dois números maiores

**Exercício 1.4:** Observe que nosso modelo de avaliação permite combinações cujos operadores são expressões compsotas. Use esta observação para descrever o comportamento do seguinte procedimento:


``` {.scheme}
(define (a-plus-abs-b a b)
  ((if (> b 0) + -) a b))
```


**Exercício 1.5:** Ben Bitdiddle inventou um teste para determinar se o intérprete que ele está enfrentando está usando uma avaliação de ordem de aplicativo ou avaliação de ordem normal. Ele define os dois procedimentos a seguir:


``` {.scheme}
(define (p) (p))

(define (test x y) 
  (if (= x 0) 
      0 
      y))
```


Então ele avalia a expressão


``` {.scheme}
(test 0 (p))
```


Que comportamento Ben observará com um intérprete que usa avaliação de ordem aplicativa? Que comportamento ele observará com um intérprete que usa avaliação de ordem normal? Explique sua resposta. (Suponha que a regra de avaliação para o formuláriio especial `if` seja a mesma, quer o interpretador esteja usando a ordem normal ou aplicativa: A expressão predicada é avaliada primeiro, e o resultado determina se é necessário avaliar a expressão consequente ou alternativa.)
